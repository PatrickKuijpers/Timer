apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

// Load keystore
def keystorePropertiesFile = file("D:\\Keystore\\keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "nl.tcilegnar.timer"
        minSdkVersion 18    // Android 4.3  (JELLY_BEAN_MR2)
        targetSdkVersion 26 // Android 8.0  (Oreo)
        versionCode 7
        versionName "0.0.7"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['signing.store.file'])
            storePassword keystoreProperties['signing.store.password']
            keyAlias keystoreProperties['signing.key.alias.timer']
            keyPassword keystoreProperties['signing.key.password.timer']
        }
        debug {
            storeFile file('../debug.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            // https://developer.android.com/studio/build/shrink-code.html
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dev"
            versionNameSuffix ".dev"
        }
        alpha {
            initWith debug
            applicationIdSuffix ".alpha"
            versionNameSuffix ".alpha"
        }
    }

    flavorDimensions "type"
    productFlavors {
        work {
            applicationIdSuffix ".work"
            dimension "type"
        }
        sleep {
            applicationIdSuffix ".sleep"
            dimension "type"
        }
    }

    // Voorkom dat errors van lint builds breken in Jenkins
    lintOptions {
        abortOnError false // TODO!
    }

    /*
    aaptOptions {
        cruncherEnabled false
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses true
            }
        }
    }
    dexOptions {
        jumboMode true
    }
    */
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // wearApp project(':wear') // Niet included omdat dex dan nodig zal zijn
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.github.satyan:sugar:1.5' // https://github.com/chennaione/sugar
    implementation 'net.hockeyapp.android:HockeySDK:4.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation 'org.mockito:mockito-all:1.9.5'
}

dexcount {
    format = "list"
    includeClasses = true
    includeFieldCount = false
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
    runOnEachAssemble = false
}
