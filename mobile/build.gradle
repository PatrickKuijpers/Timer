apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

// Load keystore
def keystorePropertiesFile = file("D:\\Keystore\\keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "nl.tcilegnar.timer"
        minSdkVersion 18    // API 4.3  (JELLY_BEAN_MR2)
        targetSdkVersion 25 // API 7.1  (N_MR1)
        versionCode 7
        versionName "0.0.7"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['signing.store.file'])
            storePassword keystoreProperties['signing.store.password']
            keyAlias keystoreProperties['signing.key.alias.timer']
            keyPassword keystoreProperties['signing.key.password.timer']
        }
        debug {
            storeFile file('../debug.keystore')
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            // https://developer.android.com/studio/build/shrink-code.html
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dev"
            versionNameSuffix ".dev"
        }
        alpha {
            initWith debug
            applicationIdSuffix ".alpha"
            versionNameSuffix ".alpha"
        }
    }
    productFlavors {
        work {
            applicationIdSuffix ".work"
        }
        sleep {
            applicationIdSuffix ".sleep"
        }
    }

    // Voorkom dat errors van lint builds breken in Jenkins
    lintOptions {
        abortOnError false // TODO!
    }

    /*
    aaptOptions {
        cruncherEnabled false
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses true
            }
        }
    }
    dexOptions {
        jumboMode true
    }
    */
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // wearApp project(':wear') // Niet included omdat dex dan nodig zal zijn
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.github.satyan:sugar:1.5' // https://github.com/chennaione/sugar
    compile 'net.hockeyapp.android:HockeySDK:4.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

dexcount {
    format = "list"
    includeClasses = true
    includeFieldCount = false
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
    runOnEachAssemble = false
}
